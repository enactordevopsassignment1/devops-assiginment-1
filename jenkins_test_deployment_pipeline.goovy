def image_version
def image = "madumalt/simple_web_app"

def stack_name = 'test-deployment-stack'
def instance_details = [:]
def instance_public_dns

def private_key = '/var/lib/jenkins/.ssh/enactor-devops-2'
def docker_setup_script = 'setup_docker_n_docker_compose.sh'
def docker_compose_service_script = 'docker-compose.yml'
def mysq_schema_script = 'simple_db.sql'

def AWS_CLI_FALSE_STATE = 255

pipeline {
    agent any
    stages {
        stage('Choose a DockerTag') {
            steps {
                script {
                    def url_v1 = "https://registry.hub.docker.com/v1/repositories/${image}/tags"
                    def list = getDockerImageTags(url_v1)
                    def versions = list.join("\n")
                    image_version = input(
                            id: 'userInput', message: 'Promote:', parameters: [
                            [$class: 'ChoiceParameterDefinition', choices: versions, description: 'Versions', name: 'version']]
                    )
                }
            }
        }
        stage('Checkout docker-compose service script') {
            steps {
                retry(3) {
                    checkout([$class: 'GitSCM', branches: [[name: '*/master']], doGenerateSubmoduleConfigurations: false, extensions: [], submoduleCfg: [], userRemoteConfigs: [[credentialsId: '58fcf397-774b-4287-ad40-49b88de738fb', url: 'https://github.com/enactordevopsassignment1/docker-compose-simple-web-app.git']]])
                }
            }
        }
        stage('Deploy a stack using aws cloudformation.') {
            steps {
                echo "Proceeding with ${image}:${image_version}."
                sh 'aws cloudformation describe-stacks'

                echo "Starting the deployment of ${stack_name}."
                sh "aws cloudformation create-stack --template-body file://${pwd()}/aws_stack.json --stack-name ${stack_name}"

                echo "Waiting till stack deployment of ${stack_name} completes."
                sh "aws cloudformation wait stack-create-complete --stack-name ${stack_name}"
                echo "Successfully deployed ${stack_name}."

                script {
                    def deployment_stack_details = sh(
                            script: "aws cloudformation describe-stacks --stack-name ${stack_name}",
                            returnStdout: true
                    ).trim()
                    def json_dsd = jsonParse(deployment_stack_details)
                    def output_array = json_dsd.Stacks[0].Outputs
                    output_array.each {
                        instance_details << [(it.OutputKey): it.OutputValue]
                        if (it.OutputKey.toString() == 'PublicDNS') {
                            instance_public_dns = it.OutputValue.toString()
                        }
                    }
                    echo "${instance_public_dns}"
                    echo "${instance_details}"
                }
            }
        }
        stage('Setup docker and docker-compose, and Deploy simple_web_app within the new instance.') {
            steps {
                script {
                    def ssh_cmd = "ssh -i ${private_key} -o StrictHostKeyChecking=no ec2-user@${instance_public_dns}"

                    // Copy setup_docker_n_docker_compose.sh file to the new instance.
                    sh "scp -i ${private_key} ${docker_setup_script} ec2-user@${instance_public_dns}:/home/ec2-user/"

                    // Copy simple_db.sql mysql schema script file to the new instance.
                    sh "scp -i ${private_key} ${mysq_schema_script} ec2-user@${instance_public_dns}:/home/ec2-user/"

                    // Copy docker-compose.ym file to the new instance.
                    sh "scp -i ${private_key} ${docker_compose_service_script} ec2-user@${instance_public_dns}:/home/ec2-user/"

                    // Make setup_docker_n_docker_compose.sh file an executable.
                    sh "${ssh_cmd} 'chmod +x ${docker_setup_script}'"

                    // Run setup_docker_n_docker_compose.sh.
                    sh "${ssh_cmd} './${docker_setup_script}'"

                    // Deploy simple_web_app using docker-compose.
                    sh "${ssh_cmd} 'docker-compose up -d'"

                    // See what are the containers running.
                    sh "${ssh_cmd} 'docker ps -a'"
                }
            }
        }
        stage('Test the deployed simple_web_app.') {
            steps {
                retry(3) {
                    sh "curl 'http://${instance_public_dns}:8080/simple_web_app/'"
                }
                retry(3) {
                    sh "curl 'http://${instance_public_dns}:8080/simple_web_app/rememberme/'"
                }
            }
        }
    }
    post {
        always {
            echo "Deleting the ${stack_name}."

            script{
                def stack_exists = false
                try{
                    stack_exists = sh(
                            script: "aws cloudformation wait stack-exists --stack-name ${stack_name}",
                            returnStatus: true
                    ) != AWS_CLI_FALSE_STATE
                } catch(err){
                    stack_exists = false
                    echo "${err}"
                }

                echo "The ${stack_name} exists ? ${stack_exists}"

                if (stack_exists) {
                    retry(3){
                        sh "aws cloudformation delete-stack --stack-name ${stack_name}"
                    }
                    sh "aws cloudformation wait stack-delete-complete --stack-name ${stack_name}"
                }
            }
            echo "Successfully deleted the ${stack_name}."
        }
        failure {
            emailext attachLog: true, body: '''Please see the attached log. Thanks.''', subject: '[Jenkins][simple_web_app] Deployment Test      Failure', to: 'madumalt@gmail.com,thilina.11@cse.mrt.ac.lk'
        }
    }
}

def getDockerImageTags(url) {
    def myjson = getUrl(url)
    def results = jsonParse(myjson)
    def tags = []
    results.each {
        tags << it.name
    }
    tags
}

def jsonParse(json) {
    new groovy.json.JsonSlurper().parseText(json)
}

def getUrl(url) {
    sh(returnStdout: true, script: "curl -s ${url} 2>&1 | tee result.json")
    def data = readFile('result.json').trim()
    data
}
